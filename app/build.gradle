apply plugin: 'com.google.gms.google-services'

android {
    defaultConfig {
        applicationId "cc.aoeiuv020.panovel"
        ndk {
            // 极光只支持这些，
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: applicationId,
                JPUSH_APPKEY : "c4d000bcea91fa33a514f72b", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "jpush_channel",
        ]
    }
    sourceSets {
        androidTest.assets.srcDirs +=
                files("$projectDir/schemas".toString())
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name == 'release') {
            variant.mergeAssets.doLast {
                delete(fileTree(dir: variant.mergeAssets.outputDir, includes: ['admob_test_device_list']))
            }
        }
    }
    buildTypes {
        release {
            useProguard true
        }
    }
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:' + constraint_layout_version
    implementation 'com.android.support:design:' + support_version
    implementation 'com.android.support:cardview-v7:' + support_version
    implementation 'com.android.support:preference-v7:' + support_version
    implementation 'com.android.support:recyclerview-v7:' + support_version
    implementation 'com.android.support:support-vector-drawable:' + support_version
    implementation 'com.google.firebase:firebase-core:' + firebase_core_version
    implementation 'com.google.firebase:firebase-ads:' + firebase_ads_version
    // firebase 15.0.1依赖的customtabs是26.0.1，不知道为什么不会自动指向新版而是会报红，
    // 所以明确指定customtabs,
    implementation 'com.android.support:customtabs:' + support_version
    implementation 'com.github.bumptech.glide:glide:' + glide_version
    kapt 'com.github.bumptech.glide:compiler:' + glide_version
    implementation 'com.miguelcatalan:materialsearchview:1.4.0'
    implementation 'com.google.code.gson:gson:' + gson_version
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.github.hackware1993:MagicIndicator:1.5.0'
    implementation project(':api')
    implementation project(':reader')
    implementation 'com.tencent.bugly:crashreport:2.6.6'
    implementation project(':server')
    implementation 'cn.jiguang.sdk:jpush:3.1.2'
    implementation 'cn.jiguang.sdk:jcore:1.2.0'
    implementation project(':baseJar')
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'android.arch.persistence.room:runtime:' + room_version
    kapt 'android.arch.persistence.room:compiler:' + room_version
    // testImplementation不行，找不到类，原因不明，
    // 不记得试过androidTestImplementation没有，
    debugImplementation 'android.arch.persistence.room:testing:' + room_version
    implementation project(':IronDB')
    implementation 'com.github.santalu:aspect-ratio-imageview:1.0.4'
    implementation 'com.jayway.jsonpath:json-path:' + json_path_version
    implementation 'com.github.AoEiuV020.AndroidDemo:okhttp:' + okhttp_ssl_version
    implementation project(':local')
}

